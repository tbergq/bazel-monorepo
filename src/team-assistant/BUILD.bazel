load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin")
load("@npm//http-server:index.bzl", "http_server")
load("@npm//relay-compiler:index.bzl", "relay_compiler")
load("@npm//vercel:index.bzl", "vercel")
load("@npm//webpack-cli:index.bzl", "webpack_cli")
load("@npm//webpack-dev-server:index.bzl", "webpack_dev_server")
load("//:eslint/eslint.bzl", "eslint")
load("//jest:jest.bzl", "jest_test")
load(":dependencies.bzl", "get_deps")

webpack_cli(
    name = "public",
    args = [
        "$(execpath src/index.js)",
        "--config",
        "$(execpath webpack.config.js)",
        "--output-path",
        "$(@D)",
        "--mode",
        "production",
    ],
    data = glob([
        "**/*.js",
        "**/*.json",
        "**/*.ico",
    ]) + get_deps() + [
        ":src/app.css",
        "//:babel.config.js",
        ":.babelrc.js",
    ],
    output_dir = True,
)

http_server(
    name = "server",
    args = [
        "src/team-assistant/public/",
        "-o",
        "-p",
        "9000",
    ],
    data = [
        ":public",
    ],
)

jest_test(
    name = "test",
    srcs = [
        "//:babel.config.js",
        ":.babelrc.js",
        ":scripts/setup-jest.js",
    ] + glob([
        "src/**/*.js",
        "**/*.json",
    ]),
    jest_config = ":jest.config.js",
    tags = [
        # Need to set the pwd to avoid jest needing a runfiles helper
        # Windows users with permissions can use --enable_runfiles
        # to make this test work
        "no-bazelci-windows",
        # TODO: why does this fail almost all the time, but pass on local Mac?
        "no-bazelci-mac",
    ],
    deps = get_deps() + [
        "@npm//relay-test-utils",
        "@npm//@testing-library/react",
        "@npm//@testing-library/user-event",
        "@npm//@testing-library/jest-dom",
        "//jest:preset",
    ],
)

webpack_dev_server(
    name = "dev_server",
    args = [
        "--entry",
        "./src/team-assistant/src/index.js",
        "--config",
        "$(execpath webpack.config.js)",
        "--port",
        "9000",
        "--hot",
        "--watch-poll",
    ],
    data = glob([
        "**/*.js",
        "**/*.json",
        "**/*.ico",
    ]) + get_deps() + [
        ":src/app.css",
        "//:babel.config.js",
        ":.babelrc.js",
    ],
)

eslint(
    name = "lint",
    data = [
        ":.eslintrc.js",
        ":src",
    ],
    entry = ":src",
)

copy_to_bin(
    name = "copy",
    srcs = ["vercel.json"],
)

vercel(
    name = "deploy",
    args = [
        "./src/team-assistant",
        "--prod",
        "--confirm",
    ],
    data = [
        ":public",
        ":vercel.json",
    ],
)

relay_compiler(
    name = "relay",
    args = [
        "--src",
        "./src/team-assistant/src",
        "--schema",
        "./src/team-assistant-gql/schema.graphql",
    ],
    data = [
        ":src",
        "//src/team-assistant-gql:schema.graphql",
    ],
)
