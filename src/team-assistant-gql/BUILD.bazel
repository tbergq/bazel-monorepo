load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin", "nodejs_binary", "nodejs_test")
load("@npm//@babel/node:index.bzl", "babel_node")
load("@npm//vercel:index.bzl", "vercel")
load("//:babel/babel.bzl", "build_cjs")
load("//:eslint/eslint.bzl", "eslint")
load("//jest:jest.bzl", "jest_test")
load(":dependencies.bzl", "get_deps")

build_cjs(
    name = "lib",
    data = glob(["src/**/*"]) + [
        ":package.json",
        "//:babel.config.js",
        "@npm//@adeira/babel-preset-adeira",
    ],
    file_path = "./src/team-assistant-gql/src",
)

nodejs_binary(
    name = "server",
    data = get_deps() + [
        ":lib",
        "@npm//@babel/runtime",
    ],
    entry_point = ":lib",
)

# Run with ibazel
babel_node(
    name = "dev_server",
    args = [
        "./src/team-assistant-gql/src",
    ],
    data = glob(["src/**/*"]) + get_deps() + [
        ":package.json",
        "//:babel.config.js",
    ],
)

jest_test(
    name = "test",
    srcs = [
        "//:babel.config.js",
    ] + glob([
        "src/**/*.js",
        "scripts/**/*.js",
    ]),
    extra_args = ["--forceExit"],
    jest_config = ":jest.config.js",
    deps = get_deps() + [
        "@npm//mongodb-memory-server",
        "@npm//@babel/runtime",
        "//src/graphql-test-utils:graphql_test_utils",
    ],
)

# Use with bazel run to update local snapshot
nodejs_test(
    name = "test_bc",
    data = get_deps() + [
        ":src",
        ":package.json",
        "//:babel.config.js",
        "@npm//@adeira/graphql-bc-checker",
        "@npm//@babel/register",
        "@npm//@babel/runtime",
        ":schema.graphql",
    ],
    entry_point = ":scripts/test-bc.js",
)

eslint(
    name = "lint",
    data = [
        ":package.json",
        ":src",
    ],
    entry = ":src",
)

copy_to_bin(
    name = "copy",
    srcs = [
        "package.json",
        "vercel.json",
    ],
)

vercel(
    name = "deploy",
    args = [
        "./src/team-assistant-gql",
        "--prod",
        "--confirm",
    ],
    data = [
        ":lib",
        ":package.json",
        ":vercel.json",
    ],
)

exports_files(
    [
        "schema.graphql",
    ],
    visibility = ["//visibility:public"],
)
